plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'java-library'
    id 'maven-publish'
    id 'groovy'
}

group = 'dev.lukebemish'

sourceSets.create("testGeneratorUsage")

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
    withJavadocJar()
    registerFeature("fabric") {
        usingSourceSet(sourceSets.create("fabric"))
        withSourcesJar()
        withJavadocJar()
    }
    registerFeature("neoforge") {
        usingSourceSet(sourceSets.create("neoforge"))
        withSourcesJar()
        withJavadocJar()
    }
    registerFeature("mixin") {
        usingSourceSet(sourceSets.create("mixin"))
        withSourcesJar()
        withJavadocJar()
    }
    registerFeature("generator") {
        usingSourceSet(sourceSets.create("generator"))
        withSourcesJar()
        withJavadocJar()
    }
    registerFeature("testGeneratorMixin") {
        usingSourceSet(sourceSets.create("testGeneratorMixin"))
        disablePublication()
    }
    registerFeature("testGeneratorTarget") {
        usingSourceSet(sourceSets.create("testGeneratorTarget"))
        disablePublication()
    }
}

dependencies {
    compileOnlyApi(libs.jetbrains.annotations)
    compileOnlyApi(libs.jspecify)
    compileOnly(libs.groovy)
    compileOnly(libs.gradle) {
        capabilities {
            requireFeature "internal"
        }
    }
    compileOnly(libs.kotlin.stdlib)

    fabricApi(project(":"))
    fabricCompileOnly(libs.groovy)
    fabricCompileOnly(libs.kotlin.stdlib)

    neoforgeApi(project(":"))
    neoforgeCompileOnly(libs.groovy)
    neoforgeCompileOnly(libs.kotlin.stdlib)

    mixinApi(project(":"))
    mixinCompileOnly(libs.groovy)
    mixinCompileOnly(libs.kotlin.stdlib)

    generatorImplementation(libs.asm.core)
    generatorImplementation(libs.asm.tree)
    generatorImplementation(project(":"))

    testGeneratorTargetApi(project(":"))
    testGeneratorTargetCompileOnly(libs.groovy)
    testGeneratorTargetCompileOnly(libs.kotlin.stdlib)

    testGeneratorMixinImplementation(project(":")) {
        capabilities {
            requireFeature("test-generator-target")
        }
    }
    testGeneratorMixinCompileOnly(libs.groovy)
    testGeneratorMixinCompileOnly(libs.kotlin.stdlib)
}

tasks.named("generatorJar", Jar) {
    manifest.attributes(
        "Main-Class": "dev.lukebemish.chronicle.generator.DslCreator"
    )
}

abstract class RunGeneratorTask extends DefaultTask {
    @OutputDirectory
    abstract DirectoryProperty getOutputDirectory()

    @Input
    abstract ListProperty<String> getEntrypoints()

    @Classpath
    @InputFiles
    abstract ConfigurableFileCollection getGeneratorClasspath()

    @Classpath
    @InputFiles
    abstract ConfigurableFileCollection getDslClasspath()

    @Input
    abstract Property<String> getOutputPackage()

    @Inject
    protected abstract ExecOperations getExecOperations()

    @TaskAction
    void run() {
        if (outputDirectory.get().asFile.exists()) {
            outputDirectory.get().asFile.deleteDir()
        }
        execOperations.javaexec {
            classpath = generatorClasspath
            mainClass.set("dev.lukebemish.chronicle.generator.DslCreator")
            def runArgs = [
                "-o", outputDirectory.get().asFile.absolutePath,
                "--package", outputPackage.get(),
                "-cp", dslClasspath.asPath
            ]
            for (String entrypoint : entrypoints.get()) {
                runArgs.add("--entrypoint")
                runArgs.add(entrypoint)
            }
            args = runArgs
        }.rethrowFailure().assertNormalExitValue()
    }
}

tasks.register('testGeneratorTask', RunGeneratorTask) {
    generatorClasspath.from(configurations.generatorRuntimeClasspath)
    generatorClasspath.from(tasks.generatorJar)
    dslClasspath.from(configurations.testGeneratorMixinRuntimeClasspath)
    dslClasspath.from(tasks.testGeneratorMixinJar)
    outputDirectory = layout.buildDirectory.dir("generated/chronicle-generator-test")
    outputPackage = "test.generated.output"
    entrypoints.add("test.generator.target.Root")
}

dependencies {
    testGeneratorUsageImplementation(files(tasks.named('testGeneratorTask', RunGeneratorTask).flatMap { it.outputDirectory }))
    testGeneratorUsageImplementation(project(":")) {
        capabilities {
            requireFeature("test-generator-mixin")
        }
    }
    testGeneratorUsageImplementation(project(":")) {
        capabilities {
            requireFeature("test-generator-target")
        }
    }
    testGeneratorUsageImplementation(libs.kotlin.stdlib)
    testGeneratorUsageImplementation(libs.groovy)
}

kotlin {
    jvmToolchain(21)
    compilerOptions {
        freeCompilerArgs.add("-Xcontext-parameters")
        freeCompilerArgs.add("-no-stdlib")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            suppressAllPomMetadataWarnings()
        }
    }
}
