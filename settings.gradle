pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            url "https://maven.lukebemish.dev/releases/"
        }
    }
    plugins {
        id 'com.gradle.plugin-publish' version '+'
    }
    includeBuild "build-logic"
}

buildscript {
    dependencyLocking {
        lockMode = LockMode.STRICT
        lockAllConfigurations()
        lockFile.set file("locks/settings-buildscript-gradle.lockfile")
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '+'
    id 'dev.lukebemish.managedversioning' version '+'
    id 'dev.lukebemish.conventions' version '+'
    id 'build-logic'

    // Since it needs to overload the one from managedversioning
    id 'dev.lukebemish.central-portal-publishing' version '+' apply false

    // Has a weird build-service related incompat with central-portal-publishing (FFS, _how_?) if you don't do this...
    id 'org.jetbrains.kotlin.jvm' version '+' apply false
}

gradle.lifecycle.beforeProject {p ->
    p.buildscript{
        dependencyLocking {
            lockMode = LockMode.STRICT
            lockAllConfigurations()
            var cleanProjectName = (p.path + ":").replace(':', ' ').trim().replace(' ', '-')
            lockFile.set rootProject.file("locks/${cleanProjectName}${cleanProjectName.empty ? '' : '-'}buildscript-gradle.lockfile")
        }
    }

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    tasks.withType(Sign).configureEach {
        // Gradle broke something in 8.8 here, and now you can't publish plugins to other mavens if signing is disabled...
        enabled System.getenv('GPG_KEY') !== null
    }

    publishing {
        publications.configureEach {
            managedVersioning.sign(signing, it)
        }
    }
}

managedVersioning {
    versionFile.set file('version.properties')
    versionPRs()
    versionSnapshots()

    publishing {
        mavenStaging()
        mavenSnapshot()
        mavenPullRequest()
        mavenCentralMakeBundle()
    }

    gitHubActions {
        release {
            prettyName = 'Release'
            workflowDispatch = true
            gradleJob {
                javaVersion.set '21'
                name.set 'build'
                buildCache()
                setupGitUser()
                readOnly = false
                gradlew 'Tag Release', 'tagRelease'
                gradlew 'Build', 'build'
                push()
                recordVersion 'Record Version', 'version'
                dependencySubmission()
            }
            gradleJob {
                javaVersion.set '21'
                name.set 'publishCentral'
                buildCache()
                needs.add('build')
                gradlew 'Publish Central', 'publish'
                tag.set('${{needs.build.outputs.version}}')
                sign()
                mavenCentral()
                mavenStaging('github')
            }
            gradleJob {
                javaVersion.set '21'
                name.set 'publishPlugins'
                buildCache()
                needs.add('build')
                gradlew 'Publish Plugins', 'publishPlugins'
                tag.set('${{needs.build.outputs.version}}')
                pluginPortal()
            }
        }
        snapshot {
            prettyName.set 'Snapshot'
            workflowDispatch.set(true)
            onBranches.add 'main'
            gradleJob {
                javaVersion.set '21'
                name.set 'build'
                buildCache()
                cacheReadOnly = false
                gradlew 'Build', 'build'
                gradlew 'Publish', 'publish'
                mavenSnapshot('github')
            }
        }
        build_pr {
            prettyName.set 'Build PR'
            pullRequest.set(true)
            gradleJob {
                javaVersion.set '21'
                name.set 'build'
                gradlew 'Build', 'build'
                gradlew 'Publish', 'publish'
                pullRequestArtifact()
            }
        }
        publish_pr {
            prettyName.set 'Publish PR'
            publishPullRequestAction(
                'github',
                "dev/lukebemish/chronicle,dev/lukebemish/chronicle/*",
                'Build PR'
            )
        }
    }
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        maven {
            name = "Gradle Releases"
            url = "https://repo.gradle.org/gradle/libs-releases"
        }
    }

    repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
}

rootProject.name = 'chronicle'

include("plugin")
project(":plugin").name = "chronicle-gradle-plugin"

