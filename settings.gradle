pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            url "https://maven.lukebemish.dev/releases/"
        }
    }
    plugins {
        id 'org.jetbrains.kotlin.jvm' version '+'
    }
    includeBuild "build-logic"
}

buildscript {
    dependencyLocking {
        lockMode = LockMode.STRICT
        lockAllConfigurations()
        lockFile.set file("locks/settings-buildscript-gradle.lockfile")
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '+'
    id 'dev.lukebemish.managedversioning' version '+'
    id 'dev.lukebemish.conventions' version '+'
    id 'build-logic'
}

gradle.lifecycle.beforeProject {p ->
    p.buildscript{
        dependencyLocking {
            lockMode = LockMode.STRICT
            lockAllConfigurations()
            var cleanProjectName = (p.path + ":").replace(':', ' ').trim().replace(' ', '-')
            lockFile.set rootProject.file("locks/${cleanProjectName}${cleanProjectName.empty ? '' : '-'}buildscript-gradle.lockfile")
        }
    }
}

managedVersioning {
    versionFile.set file('version.properties')
    versionPRs()
    versionSnapshots()
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        maven {
            name = "Gradle Releases"
            url = "https://repo.gradle.org/gradle/libs-releases"
        }
    }

    repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
}

rootProject.name = 'chronicle'

include("plugin")
project(":plugin").name = "chronicle-gradle-plugin"

